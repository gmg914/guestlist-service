apply plugin: 'java'
apply plugin: 'gradle-one-jar'
apply plugin: 'application'

// Use Java 7 by default
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'com.guestlist.GuestListApplication'

// Add Gradle OneJar Plugin, see https://github.com/rholder/gradle-one-jar
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.3'
    }
}

// Set our project variables
project.ext {
    dropwizardVersion = '0.7.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
       'io.dropwizard:dropwizard-core:' + dropwizardVersion,
       'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
       'io.dropwizard:dropwizard-migrations:' + dropwizardVersion,
       'io.dropwizard:dropwizard-auth:' + dropwizardVersion,
       'io.dropwizard:dropwizard-validation:' + dropwizardVersion,
       'io.dropwizard:dropwizard-lifecycle:' + dropwizardVersion,
       'org.apache.commons:commons-lang3:3.3.2',
       'org.elasticsearch:elasticsearch:1.4.4',
       'com.google.code.gson:gson:2.3.1'
    )
}

// Configure the oneJar task
task oneJar(type: OneJar) {
    mainClass = mainClassName
}

// Configure the run task to start the Dropwizard service
run {
    args 'server', './src/dist/config/guestlist.yml'

}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}

artifacts {
    oneJar
}
